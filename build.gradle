buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.townsfolk:gradle-release:1.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "maven-publish"

repositories {
    jcenter()
}

dependencies {
    compile "io.dropwizard:dropwizard-core:0.7.1"
    compile "org.mongodb:mongo-java-driver:2.12.3"
    compile "com.commercehub.jackson:jackson-datatype-mongo:1.1.0"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
    extension "jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

group = "com.commercehub.dropwizard"
sourceCompatibility = "1.7"

apply plugin: "release"
apply plugin: "com.jfrog.bintray"

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = "main"
        userOrg = "commercehub-oss"
        name = "dropwizard-mongo"
        desc = "A library that supports using MongoDB for data persistence in Dropwizard applications."
        licenses = ['Apache-2.0']
        labels = ["dropwizard", "mongodb", "mongo"]
    }
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar
createReleaseTag.dependsOn bintrayUpload
